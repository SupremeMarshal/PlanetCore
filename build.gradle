buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.6-SNAPSHOT'
    }
}
plugins {
    id("io.github.opencubicchunks.gradle.fg2fixed")
}

apply plugin: 'org.spongepowered.mixin'


//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "1.3.0"
group = "com.PlanetCore.planetcore" 
archivesBaseName = "planetcore"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"

    coreMod = "com.PlanetCore.asm.PlanetCoreFMLLoadingPlugin"

    def args = [
            "-Dmixin.debug.export=true",
            "-Dmixin.debug.verbose=true",
    ]
    clientJvmArgs += args
    serverJvmArgs += args

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_39"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

idea {
    module {
        inheritOutputDirs = true
    }
}

repositories {
    maven { //needed for CubicWorldGen
        name = "JitPack"
        url = "https://jitpack.io/"
    }

    maven {
        url = "https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/"
    }


}

dependencies {
    deobfProvided ("com.github.OpenCubicChunks.CubicChunks:cubicchunks:0d61d787ed052ad53391c3ebe9799e4ed2da3659") {
        transitive = false
    }
    deobfProvided ("com.github.OpenCubicChunks:CubicWorldGen:27de56d2f792513873584b2f8fd9f3082fb259ec") {
        transitive = false
    }
    deobfCompile('software.bernie.geckolib:forge-1.12.2-geckolib:3.0.14')

    compile ("org.spongepowered:mixin:0.8.1")
}

mixin {
    add sourceSets.main, "mixins.planetcore.refmap.json"
}

sourceSets {
    main {
        ext.refMap = "mixins.planetcore.refmap.json"
    }
}

jar {
    manifest {
        attributes(
                "MixinConfigs": "mixins.planetcore.json",
                "tweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": 0,
                "FMLCorePlugin": "com.PlanetCore.asm.PlanetCoreFMLLoadingPlugin",
                "FMLCorePluginContainsFMLMod": "true",
                "ForceLoadAsMod": "true",
        )
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
